plugins {
    id 'java'
    id 'org.springframework.boot'
    id "io.freefair.lombok"
    id 'jacoco'
    id 'pmd'
    id 'project-report'
    id 'build-dashboard'
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'build-dashboard'

repositories {
    mavenCentral()
}

group 'wirednerd'
version '1.0.0'

dependencies {
    implementation 'org.springframework.batch:spring-batch-core'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.netbeans.api:org-netbeans-api-annotations-common:RELEASE126'

    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mongounit:mongounit:2.0.1'
}

javadoc {
    excludes = [
            'wirednerd/springbatch/mongo/Application**',
            'wirednerd/springbatch/mongo/MongodbRepositoryConstants**'
    ]
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 0.95
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.85
            }
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.95
            }
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.90
            }
            includes = [
                    'wirednerd.springbatch.mongo.*'
            ]
            excludes = [
                    'wirednerd.springbatch.mongo.Application'
            ]
        }
    }
}

pmd {
    ignoreFailures = true
    consoleOutput = true
    ruleSetFiles = files("custom-pmd-ruleset.xml")
    ruleSets = []
}
pmdTest.enabled = false


htmlDependencyReport {
    projects = project.allprojects
}

buildDashboard {
    dependsOn htmlDependencyReport
}

task sourcesJar(type: Jar) {
    description = 'Assembles a jar archive containing the main sources.'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description = 'Assembles a jar archive containing the javadoc.'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}
plugins {
    id 'java-library'
    id "io.freefair.lombok" version "latest.release"
    id 'pmd'
    id 'info.solidsoft.pitest' version "latest.release"
    id 'signing'
    id 'maven-publish'
}

apply plugin: 'java-library'

repositories {
    mavenCentral()
}

dependencies {
    api project(":spring-batch-document")
    implementation 'org.springframework.batch:spring-batch-core:latest.release'
    implementation 'org.springframework.data:spring-data-mongodb:[3.2,3.3)'
    // version 3.2 for compatibility with boot 2.5

    // Fix warning: unknown enum constant javax.annotation.meta.When.MAYBE
    implementation 'org.netbeans.api:org-netbeans-api-annotations-common:latest.release'

    testImplementation 'org.junit.jupiter:junit-jupiter:latest.release'
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springbootVersion}"
    testImplementation 'org.mongodb:mongodb-driver-sync:latest.release'
}

compileJava {
    options.release = 11
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.addBooleanOption('html5', true)
    excludes = [
            'io/github/wirednerd/springbatch/mongo/MongodbRepositoryConstants**'
    ]
}

test {
    systemProperties = System.properties
    useJUnitPlatform()
}

pmd {
    ignoreFailures = true
    consoleOutput = true
    ruleSetFiles = files("custom-pmd-ruleset.xml")
    ruleSets = []
}
pmdTest.enabled = false

pitest {
    targetClasses = ['io.github.wirednerd.springbatch.mongo.*']
    threads = 8
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    junit5PluginVersion = '0.15'
}

// disables building plain.jar
jar {
    enabled = true
    archiveClassifier = '' //use empty string
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "io.github.wirednerd:spring-batch-mongo"
                description = 'Store Spring Batch Execution Data in MongoDB'
                url = 'https://github.com/WiredNerd/spring-batch-mongo'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'wirednerd'
                        name = 'Peter Busch'
                        organizationUrl = 'https://github.com/WiredNerd'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:WiredNerd/spring-batch-mongo.git'
                    developerConnection = 'scm:git@github.com:WiredNerd/spring-batch-mongo.git'
                    url = 'https://github.com/WiredNerd/spring-batch-mongo'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            def snapshotsRepoUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            name = "OSSRH"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = findProperty("OSSRH_USERNAME")
                password = findProperty("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    def signingKey = findProperty("SIGNING_SIGNINGKEY")
    def signingPassword = findProperty("SIGNING_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
